buildscript {
    repositories {
        mavenCentral()
    }
}
plugins {
    id "java-library"
    id "io.spring.dependency-management" version "1.1.0"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'xyz.jpenilla.run-paper' version '1.0.6'
    id "io.fairyproject" version "0.6.8b2-SNAPSHOT"
}

group = "dev.craftplugins"
version = "1.1.1"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name "Jitpack"
        url "https://jitpack.io"
    }
    maven {
        name "Imanity"
        url "https://repo.imanity.dev/imanity-libraries"
    }
    maven {
        name "PaperMC"
        url "https://repo.papermc.io/repository/maven-public"
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT")

    // Jabel
    annotationProcessor("com.github.bsideup.jabel:jabel-javac-plugin:0.4.2")
    compileOnly("com.github.bsideup.jabel:jabel-javac-plugin:0.4.2")
}

dependencies {
    api("io.fairyproject:bukkit-bundles")
    api("io.fairyproject:module.animation")
    api("io.fairyproject:bukkit-command")
    api("io.fairyproject:bukkit-menu")
    api("io.fairyproject:module.hologram")
    api("io.fairyproject:module.config")
    api("io.fairyproject:bukkit-xseries")
    api("io.fairyproject:bukkit-items")
    api("io.fairyproject:module.nametag")
    api("io.fairyproject:module.sidebar")
    api("io.fairyproject:bukkit-visibility")
    api("io.fairyproject:bukkit-visual")
    api("io.fairyproject:bukkit-timer")
    api("io.fairyproject:bukkit-nbt")
    api("io.fairyproject:module.tablist")
    api(libs.bundles.configurate)
}

fairy {
    mainPackage.set(findProperty("plugin.package").toString())
    name.set("CraftCore")

    bukkitProperties().bukkitApi = "1.13"
}

configure([tasks.compileJava]) {
    sourceCompatibility = 17 // for the IDE support
    options.release = 8
    options.encoding = "UTF-8"

    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

shadowJar {
    relocate("io.fairyproject", "" + findProperty("plugin.package") + ".fairy")
    relocate("net.kyori", "" + findProperty("plugin.package") + ".fairy.libs.kyori")
    relocate("com.github.retrooper.packetevents", "" + findProperty("plugin.package") + ".fairy.libs.packetevents")
    relocate("org.yaml.snakeyaml", "" + findProperty("plugin.package") + ".snakeyaml")
    relocate("com.cryptomorin.xseries", "" + findProperty("plugin.package") + ".xseries")
    relocate("org.spongepowered.configurate", "" + findProperty("plugin.package") + ".libs.configurate")
    relocate("io.leangen.geantyref", "" + findProperty("plugin.package") + ".libs.geantyref")
    relocate("org.spongepowered.configurate", "" + findProperty("plugin.package") + ".libs.configurate")

    archiveFileName.set("CraftCore.jar")
}

build {
    dependsOn(shadowJar)
}

test {
    useJUnitPlatform()
}

runServer {
    minecraftVersion("1.19.4")
    legacyPluginLoading()
    args("--nogui")
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }
}